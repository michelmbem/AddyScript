function equation(a, b, c)
{
	if (a == 0)
	{
		if (b == 0)
		{
			if (c == 0)
				println('any number is a solution');
			else
				println('no solution');
		}
		else
		{
			x = -c / b;
			println($'unique solution: {x}');
		}
	}
	else
	{
		delta = b ** 2 - 4 * a * c;
		
		if (delta == 0)
		{
			x = -b / (2 * a);
			println($'double solution: {x}');
		}
		else if (delta > 0)
		{
			x1 = (-b - sqrt(delta)) / (2 * a);
			x2 = (-b + sqrt(delta)) / (2 * a);
			println($'a pair of solutions: {x1} and {x2}');
		}
		else
		{
			r = -b / (2 * a);
			i = sqrt(-delta) / (2 * a);
			x1 = complex::of(r, i);
			x2 = complex::of(r, -i);
			println($'complex conjugate solutions: {x1} and {x2}');
		}
	}
}

function dialog()
{
	println('solving a*x**2 + b*x + c = 0:');
	println('-----------------------------');
	
	a = (float)readln('a = ');
	b = (float)readln('b = ');
	c = (float)readln('c = ');
	
	return (a, b, c);
}

function getOption(message, options, defaultOption)
{
	var selectedOption;
	var printedOptions = '/'.join(..options.select(|opt| => opt == defaultOption ? opt.toUpper() : opt));
	
	do
		selectedOption = readln($'{message} ({printedOptions}) ').toLower().trim() ?? defaultOption;
	while (selectedOption not in options);
	
	println();
	
	return selectedOption;
}

do
	equation(..dialog());
while ('y' == getOption('continue ?', ['y', 'n'], 'y'));