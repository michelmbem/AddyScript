record Box(width, height, depth);
record Employee(name, age, salary, dateHired);

record Developper(name, age, salary, dateHired, languages) {
	public static function fromEmployee(emp, lang) => new Developper(..emp.__members, lang);
	public function toEmployee() => new Employee(..this.__members[..4]);
}

record Vector(..items) {
	public property [] => this.items[__key];
	public property size => this.items.size;
	
	public function iterator() {
		foreach (item in this.items) yield item;
	}
}

/*********************************************************************************/

res = int(readln('type a number: ')) switch {
	not >= 0 => 'negative',
	>= 5 and < 10 => {
		print('yeah i know, ');
		println('it\'s between 5 and 9!');
		yield $'you typed {__value}';
	},
	0 or 1 or 2 or 3 or 4 => 'from 0 to 4',
	_ => '10 and above'
};
println(res);

try {
	res = readln('\ntype some text: ') switch {
		matches @'/[+-]?\d+(\.\d+)?(e[+-]?\d+)?/i' when decimal(__value) > 0 =>
			$'{__value} is a positive number',
		matches @'/[+-]?\d+(\.\d+)?(e[+-]?\d+)?/i' => $'{__value} is just a number',
		_ => throw $'{__value} is not a number'
	};
	println(res);
} catch (e) {
	println($'\n{e.name} : {e.message}');
}

l = [
	new Employee('Franklin Kenyata', 44, 5000, `2010-01-14`),
	new Employee('Jeanette Diabaté', 32, 3500, `2014-09-30`),
	new Developper('Dereck Musa', 44, 4200, `2010-07-28`, ['C++', 'C#', 'Java']),
	null,
	'hello john',
	new Box(15, 7, 9),
	new Vector(4, 7, 18),
	new Exception('this is an exception'),
	'hello, salif',
	[4, randint(10), now()],
	new Box(12, 8, 4),
	new Vector(4, 1.5, 'Hi!')
];

l += [
	l[0] with { name = 'Ahmadu Suleyman', salary = 7000 },
	Developper::fromEmployee(l[1], ['Scala', 'Swift']) with { name = 'Béatrice Kitoko' },
	l[2] with { name = 'Kim Ondo', age = 36, languages = ['Python', 'TypeScript'] },
	l[2].toEmployee() with { name = 'Ishmael Ibrahim', salary = 3700 }
];

println('\n\nMatching items in a list:');
println('-------------------------');

l.each(|item| => {
	res = item switch {
		null => 'this item is null',
		Employee or Developper and {age : 44, dateHired.year: 2010} =>
			$'{item.name} is 44 years old employee hired in 2010 that earns ${item.salary}/month',
		(4, int and <= 10, _) => 'a sequence of 3 that starts with 4 followed by an integer that\'s <= 10',
		Box(width, depth) => $'a box of width: {width} and depth: {depth}',
		$'hello {name}' => $'a warm greating to a certain {name}',
		_ => 'unmatched'
	};
       	
	println($'\n{item}');
	println($'> {res}');
});

println();

foreach (item in l) {
	var name, languages;
	if (item is Developper(name, languages))
		println($'{name} is a {" and ".join(..languages)} developper');
}

readln();