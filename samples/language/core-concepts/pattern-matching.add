class Employee
{
	public constructor(name, age, salary, dateHired)
	{
		this.name = name;
		this.age = age;
		this.salary = salary;
		this.dateHired = dateHired;
	}
	
	public property name;
	
	public property age;
	
	public property salary;
	
	public property dateHired;
	
	public function toString(fmt = '') =>
		$'Employee {{name: {this.name}, age: {this.age}, salary: {this.salary}, dateHired: {this.dateHired:d}}}';
}

class Developper : Employee
{
	public constructor(name, age, salary, dateHired, languages)
		: super(name, age, salary, dateHired)
	{
		this.languages = languages;
	}
	
	public property languages;
	
	public function toString(fmt = '') =>
		$'Developper {{name: {this.name}, age: {this.age}, salary: {this.salary}, dateHired: {this.dateHired:d}, languages: {"|".join(..this.languages)}}}';
}

final class Box
{
	public constructor (width, height, depth)
	{
		this.width = width;
		this.height = height;
		this.depth = depth;
	}
	
	public property width { read; private write; }
	
	public property height { read; private write; }
	
	public property depth { read; private write; }
	
	public function toString(fmt = '') =>
		$'Box {{width: {this.width}, height: {this.height}, depth: {this.depth}}}';
}

class Vector
{
	public constructor (..items)
	{
		this.__items = items;
	}
	
	public property size => this.__items.size;
	
	public property [] => this.__items[__key];
	
	public function iterator()
	{
		foreach (item in this.__items)
			yield item;
	}
	
	public function toString(fmt = '') => 'Vector ' + this.__items.toString(fmt);
}

o = new {name = 'my object', size = 18, color = 'blue'};
res = o switch {
	null => 'null',
	5 => 'five',
	{height : 150, color : 'blue'} => 'a blue and 150 tall object',
	{name : 'my object', size : 18} => 'my object of size 18'
};
println($'o is {o}');
println($'result with o: {res}');
println();

res = PI switch {
	3.14 => 'three-point-fourteen',
	>= 3 and < 4 => 'between 3 (inclusive) and 4 (exclusive)',
	decimal => 'a decimal number'
};
println($'result with PI: {res}');
println();

res = ((int)readln('type a number: ')) switch {
	< 0 => 'negative',
	0 or 1 or 2 or 3 or 4 => 'from 0 to 4',
	not >= 10 => {
		print('yeah i know, ');
		println('it\'s between 5 and 9!');
		yield __value;
	},
	_ => '10 and above'
};
println($'result: {res}');
println();

res = readln('type some text: ') switch {
	matches @'/[+-]?\d+(\.\d+)?(e[+-]?\d+)?/i' when decimal(__value) > 0 =>
		$'{__value} is a positive number',
	matches @'/[+-]?\d+(\.\d+)?(e[+-]?\d+)?/i' => $'{__value} is just a number',
	_ => $'{__value} is not a number'
};
println($'result: {res}');
println();

l = [
	new Employee('Franklin Kenyata', 44, 5000, `2010-01-14`),
	new Employee('Jeanette DiabatÃ©', 32, 3500, `2014-09-30`),
	new Developper('Dereck Musa', 44, 4200, `2010-07-28`, ['C++', 'C#', 'Java']),
	null,
	new Box(15, 7, 9),
	new Vector(4, 7, 18),
	new Exception('this is an exception'),
	[4, randint(10), now()],
	new Box(12, 8, 4),
	new Vector(4, 1.5, 'Hi!')
];

l.add(l[0] with { name = 'Ahmadu Suleyman', salary = 7000 });
l.add(l[2] with { name = 'Kim Ondo', age = 36, languages = ['Python', 'TypeScript'] });

l.each(|item| => {
	res = item switch {
		null => 'this item is null',
		Employee {age : 44, dateHired : {year: 2010}} =>
			$'{item.name} is 44 years old employee hired in 2010 that earns ${item.salary}/month',
		(4, int and <= 10, _) => 'a sequence of 3 that starts with 4',
		Box(width, depth) => $'a box of width: {width} and depth: {depth}',
		_ => 'unmatched'
	};
	println($'item = {item}');
	println($'result with item: {res}');
	println('-' * 110);
});
println();

l.where(|item| => item is Employee {languages : {}})
	.each(|item| => println($'{item.name} is a {" and ".join(..item.languages)} developper'));
println();

try {
	randint(-5, 5) switch {
		>= 0 => println($'{__value} is OK!'),
		_ => throw 'I don\'t like negative values'
	};
} catch (e) {
	println($'{e.name} : {e.message}');
}

readln();