abstract class Geometry
{
	public abstract function perimeter();
	
	public abstract function surface();
}

class Rectangle : Geometry
{
	public constructor(length, width)
	{
		(this.length, this.width) = (length, width);
	}
	
	public property length { read; private write; }
	
	public property width { read; private write; }
	
	public function perimeter() => 2 * (this.length + this.width);
	
	public function surface() => this.length * this.width;
	
	public function toString(fmt = '') =>
		$'Rectangle {{ length: {this.length}, width: {this.width} }}';
}

class Square : Rectangle
{
	public constructor(side) : super(side, side) {}
	
	public function toString(fmt = '') => $'Square {{ side: {this.length} }}';
}

class Ellipse : Geometry
{
	public constructor(a, b)
	{
		(this.a, this.b) = (a, b);
	}
	
	public property a { read; private write; }
	
	public property b { read; private write; }
	
	public function perimeter() => 2 * PI * sqrt((this.a ** 2 + this.b ** 2) / 2);
	
	public function surface() => PI * this.a * this.b;
	
	public function toString(fmt = '') =>
		$'Ellipse {{ semi-major-axis: {this.a}, semi-minor-axis: {this.b} }}';
}

class Circle : Ellipse
{
	public constructor(radius) : super(radius, radius) {}
	
	public function toString(fmt = '') => $'Circle {{ radius: {this.a} }}';
}

geometries = [null] * 5;
geometries[0] = new Rectangle(6, 4);
geometries[1] = new Square(5);
geometries[2] = new Ellipse(5, 4);
geometries[3] = new Circle(3);

try
{
	geometries.add(new Geometry());
	println('if you see this message, then something went wrong');
}
catch (e)
{
	println(e.name + ' : ' + e.message);
	println();
}

foreach (geometry in geometries)
{
	println(title = (geometry ?? 'null') + ':');
	println('-' * title.length);
	println(' - perimeter = {0:0.##}', geometry?.perimeter());
	println(' - surface = {0:0.##}', geometry?.surface());
	println();
}

readln();